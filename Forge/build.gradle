import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'net.minecraftforge.gradle' version '6.+'
    id 'idea'
    id 'net.darkhax.curseforgegradle' version '1.+'
    id 'com.modrinth.minotaur' version '2.+'
}

archivesBaseName = "${mod_id}-forge-${forge_supported_versions}"

minecraft {
    mappings channel: 'official', version: forge_minecraft_version

    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modClientRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modServerRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }

    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_minecraft_version}-${forge_version}"
    compileOnly project(":Common")

    // JEI
    if (project.hasProperty('jei_version')) {
        compileOnly(fg.deobf("mezz.jei:jei-${forge_minecraft_version}-common-api:${jei_version}"))
        compileOnly(fg.deobf("mezz.jei:jei-${forge_minecraft_version}-forge-api:${jei_version}"))
        compileOnly fg.deobf("me.shedaniel:REIPluginCompatibilities-forge-annotations:${rei_version}")
        if (project.hasProperty('enable_jei') && project.enable_jei.toBoolean()) {
            runtimeOnly(fg.deobf("mezz.jei:jei-${forge_minecraft_version}-forge:${jei_version}"))
        }
    }

    // REI
    if (project.hasProperty('rei_version') &&
            project.hasProperty('cloth_config_version') &&
            project.hasProperty('architectury_version')) {
        compileOnly fg.deobf("me.shedaniel:RoughlyEnoughItems-api-forge:${rei_version}")
        compileOnly fg.deobf("me.shedaniel:RoughlyEnoughItems-default-plugin-forge:${rei_version}")
        implementation fg.deobf("me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}")
        implementation fg.deobf("dev.architectury:architectury-forge:${architectury_version}")
        if (project.hasProperty('enable_rei') && project.enable_rei.toBoolean()) {
            runtimeOnly fg.deobf("me.shedaniel:RoughlyEnoughItems-forge:${rei_version}")
        }
    }

    // EMI
    if (project.hasProperty('emi_version')) {
        compileOnly fg.deobf("curse.maven:emi-580555:${emi_version}")
    //    compileOnly "dev.emi:emi-forge:${emi_version}+${forge_minecraft_version}"
        if (project.hasProperty('enable_emi') && project.enable_emi.toBoolean()) {
    //        runtimeOnly "dev.emi:emi-forge:${emi_version}+${forge_minecraft_version}"
            runtimeOnly fg.deobf("curse.maven:emi-580555:${emi_version}")
        }
    }

    // JADE
    if (project.hasProperty('jade_version')) {
        implementation fg.deobf("curse.maven:jade-324717:${jade_version}")
    }

    // CATALOGUE
    if (project.hasProperty('catalogue_version')) {
        implementation fg.deobf("curse.maven:catalogue-459701:${catalogue_version}")
    }

    // GREEK FANTASY https://www.curseforge.com/minecraft/mc-mods/greek-fantasy
//    runtimeOnly fg.deobf("curse.maven:greek-fantasy-413724:4066828")

}

tasks.withType(JavaCompile).configureEach {
    source(project(":Common").sourceSets.main.allSource)
}

processResources {
    from project(":Common").sourceSets.main.resources
}

jar.finalizedBy('reobfJar')

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = file('../../API Tokens/justenoughbreeding.md')
    def projectId = "${cf_id}".toString()
    def mainFile = upload(projectId, file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")) as Object
    mainFile.changelogType = 'markdown'
    mainFile.changelog = file('changelog.md')
    mainFile.addGameVersion('Client', 'Server')
    mainFile.releaseType = "${release_type}"
    "${forge_release_versions}".split(',').each {
        mainFile.addGameVersion("${it}")
    }
    if (project.hasProperty('jei_version')) {
        mainFile.addOptional("jei")
    }
    if (project.hasProperty('rei_version')) {
        mainFile.addOptional("roughly-enough-items")
    }
    if (project.hasProperty('emi_version')) {
        mainFile.addOptional("emi")
    }
    mainFile.addOptional("aquaculture")
    mainFile.addOptional("sophisticated-wolves")
    mainFile.addOptional("the-twilight-forest")
    mainFile.addOptional("quark")
    mainFile.addOptional("waddles")
    mainFile.addOptional("blue-skies")
    mainFile.addOptional("chileancraft")
    mainFile.addOptional("autumnity")
    mainFile.addOptional("apple-cows")
    mainFile.addOptional("snow-pig")
    mainFile.addOptional("greek-fantasy")
    mainFile.addOptional("frozen-up")
    mainFile.addOptional("catalogue")
    mainFile.addOptional("frostrealm")
    mainFile.addOptional("caracal-mod")
    mainFile.addOptional("kiwi-boi")
    mainFile.addOptional("steves-vanilla")
    mainFile.addOptional("snuffles")
    mainFile.addOptional("lil-wings")
    mainFile.addOptional("ecologics")
    mainFile.addOptional("ydms-ducks-mod")
    mainFile.addOptional("ydms-glare")
    mainFile.addOptional("duckling")
    mainFile.addOptional("friends-and-foes-forge")
    mainFile.addOptional("ydms-red-panda")
    mainFile.addOptional("naturalist")
    mainFile.addOptional("the-ulterlands")
    mainFile.addOptional("biome-backlog")
    mainFile.addOptional("marine-iguana")
    mainFile.addOptional("ydms-ostrich")
    mainFile.addOptional("ydms-fennec-fox")
    mainFile.addOptional("recrafted-creatures")
    mainFile.addOptional("earth-mobs")
    mainFile.addOptional("biome-makeover-forge")
    mainFile.addOptional("alexs-caves")
    mainFile.addOptional("untamedwilds")
    mainFile.addOptional("alexs-mobs")
    mainFile.addOptional("chococraft")
    mainFile.addOptional("ice-and-fire-dragons")
    mainFile.addOptional("aether")
    mainFile.addOptional("aether-redux")
    mainFile.addOptional("deep-aether")
    mainFile.addOptional("call-of-the-wild")
    mainFile.addOptional("creatures-and-beasts")
    mainFile.addOptional("winter-overhaul")
    mainFile.addOptional("critters-and-companions")
    mainFile.addOptional("ambient-additions")
    mainFile.addOptional("terrafirmacraft")
}

modrinth {
    def tokenText = file('../../API Tokens/modrinth.md')
    if (tokenText.exists()) {
        token = tokenText.text
    } else {
        token = ""
    }
    projectId = "${modrinth_id}"
    versionName = getArchivesBaseName() + "-" + getVersion()
    versionType = "${release_type}"
    changelog = file('changelog.md').text
    uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
    gameVersions = "${forge_release_versions}".split(",") as List<String>
    dependencies {
        if (project.hasProperty('jei_version')) {
            optional.project "jei"
        }
        if (project.hasProperty('rei_version')) {
            optional.project "rei"
        }
        if (project.hasProperty('emi_version')) {
            optional.project "emi"
        }
        optional.project "quark"
        optional.project "blue-skies"
        optional.project "snowpig"
        optional.project "caracal_mob"
        optional.project "kiwi-boi"
        optional.project "snuffles"
        optional.project "lil-wings"
        optional.project "ecologics"
        optional.project "the-ducks"
        optional.project "ydms-glare"
        optional.project "duckling"
        optional.project "friends-and-foes-forge"
        optional.project "ydms-red-panda"
        optional.project "naturalist"
        optional.project "ydms-ostrich"
        optional.project "ydms-fennec-fox"
        optional.project "earthmobs"
        optional.project "biome-makeover"
        optional.project "alexs-caves"
        optional.project "alexs-mobs"
        optional.project "chococraft"
        optional.project "ice-and-fire-dragons"
        optional.project "aether"
        optional.project "the-aether-redux"
        optional.project "deep-aether"
        optional.project "creatures-and-beasts"
        optional.project "winter-overhaul"
        optional.project "critters-and-companions"
        optional.project "terrafirmacraft"
    }
}
